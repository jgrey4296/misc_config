;;
;; Functions to enable unit testing an org file
;; TODO: put parse-tests and run-tests in variables,
;; and use a macro to expand them, allowing the two to be defined together
;; simplifying the addition of new tests
;;
(cl-defstruct org-unit-test-layer/test-extracts start end heading link text)
(cl-defstruct org-unit-test-layer/test-group name tests link bound start)
(cl-defstruct org-unit-test-layer/test type locator value)
;; Current test types:
'(:section-check :length-check :order-check :citation-check :mention-check :codeblock-check :tag-check)
(cl-defstruct org-unit-test-layer/test-results name results link)

(defvar org-unit-test-layer/heading-regexp "^*+ %s$"
  "Regexp to format when searching for a location")

(defvar org-unit-test-layer/src-block-regexp "^ +#\\+begin_src"
  "Regexp to find src blocks")

(defun org-unit-test-layer/test-to-string (test)
  (let ((tt (org-unit-test-layer/test-type test))
        (loc (org-unit-test-layer/test-locator test))
        (val (org-unit-test-layer/test-value test))
        )
    (cond
     ((eq tt :section-check ) (format "%s should have section %s." loc val))
     ((eq tt :length-check ) (format "%s should be %s than %s %s." loc (car val) (cadr val) (caddr val)))
     ((eq tt :order-check ) (format "%s should precede %s." loc val))
     ((eq tt :citation-check ) (format "%s should cite:%s." loc (if (listp val) (mapconcat 'identity val ",") val)))
     ((eq tt :mention-check ) (format "%s should mention \"%s\"." loc val))
     ((eq tt :codeblock-check ) (format "%s should have a codeblock%s." loc (if val (format "in %s" val) "")))
     ((eq tt :tag-check)   (format "%s should have tag \"%s\"." loc val))
     (t (error "unrecognized test type"))
     )
    )
  )

;; Test retrieval
(defun org-unit-test-layer/find-tests ()
  "Iterate through current org file, extracting any __org-unit-test-layer__ drawers contents."
  (save-excursion
    (goto-char (point-min))
    (org-map-entries 'org-unit-test-layer/extract-tests-from-entry)
    )
  )
(defun org-unit-test-layer/extract-tests-from-entry ()
  (let ((start (point)) end text heading element link)
    (setq heading (substring-no-properties (org-get-heading t t t t))
          link (org-store-link nil)
          end (plist-get :contents-end (cadr (org-element-at-point))))
    (if (and (re-search-forward org-drawer-regexp end t)
             (s-equals? "__org-unit-test-layer__" (match-string 1))
             (s-equals? heading (org-get-heading t t t t)))
        (progn (setq element (cadr (org-element-at-point))
                     text (string-trim (buffer-substring-no-properties (plist-get element :contents-begin)
                                                                       (plist-get element :contents-end))))
               (if (not (string-empty-p text))
                   (make-org-unit-test-layer/test-extracts :start start :end end :heading heading :link link :text text)
                 nil)
               )
      nil
      )))
;; Test Parsing
(defun org-unit-test-layer/parse-tests (data)
  ;; split text into tests by full stops, defined groups,
  ;; and their originating heading
  (assert (org-unit-test-layer/test-extracts-p data))
  (if (not (string-empty-p (string-trim (org-unit-test-layer/test-extracts-text data))))
      (let ((testgroups (parsec-with-input (org-unit-test-layer/test-extracts-text data)
                          (parsec-many-till (org-unit-test-layer/parse-group) (parsec-eof))))
            (link (org-unit-test-layer/test-extracts-link data))
            (bound (org-unit-test-layer/test-extracts-end data))
            (start (org-unit-test-layer/test-extracts-start data))
            )
        (mapc (lambda (x)
                (assert (org-unit-test-layer/test-group-p x) t)
                (setf (org-unit-test-layer/test-group-link x) link
                      (org-unit-test-layer/test-group-bound x) bound
                      (org-unit-test-layer/test-group-start x) start)) testgroups)
        testgroups
        )
    nil)
  )

(defun org-unit-test-layer/parse-space ()
  (parsec-optional* (parsec-re "[[:blank:]]*")))
(defun org-unit-test-layer/parse-word ()
  (parsec-return (parsec-re "[[:alnum:]]+")
    (org-unit-test-layer/parse-space))
  )
(defun org-unit-test-layer/parse-sword (word &optional retval)
  (let ((val (parsec-return (parsec-string word)
               (org-unit-test-layer/parse-space))))
    (if retval retval val))
  )
(defun org-unit-test-layer/parse-quote-string ()
  (mapconcat 'identity (parsec-between (parsec-ch ?\")
                                       (parsec-ch ?\")
                                       (parsec-many1 (org-unit-test-layer/parse-word)))
             " ")
  )

(defun org-unit-test-layer/parse-group-name ()
  (if (parsec-peek-p (parsec-re "[^:]+:\n"))
      (mapconcat 'identity
                 (parsec-many-till (org-unit-test-layer/parse-word) (parsec-re ":"))
                 " ")
    "Anon Group")
  )

(defun org-unit-test-layer/parse-group ()
  (org-unit-test-layer/parse-space)
  (let* ((groupname (org-unit-test-layer/parse-group-name))
         (tests (parsec-many-till (org-unit-test-layer/parse-test) (parsec-or (parsec-eof) (parsec-re "\n\n"))))
        )
    (make-org-unit-test-layer/test-group :name groupname :tests tests)
    )
  )
(defun org-unit-test-layer/parse-test ()
  (parsec-re "\n?")
  (org-unit-test-layer/parse-space)
  (let ((locator (parsec-or (org-unit-test-layer/parse-quote-string)
                            (org-unit-test-layer/parse-sword "Document" :document)
                            (org-unit-test-layer/parse-sword "A section" :section)))
        keyword test
        )
    (org-unit-test-layer/parse-space)
    (org-unit-test-layer/parse-sword "should")
    (setq keyword (org-unit-test-layer/parse-word)
          test (cond
                ((equal "have" keyword) (org-unit-test-layer/parse-ownership-test))
                ((equal "be" keyword)   (org-unit-test-layer/parse-existence-test))
                (t                      (org-unit-test-layer/parse-misc-test keyword) )
                ))
    (setf (org-unit-test-layer/test-locator test) locator)
    test))

(defun org-unit-test-layer/parse-ownership-test ()
  ;; section/tag/codeblock
  (parsec-or (org-unit-test-layer/parse-section-check)
             (org-unit-test-layer/parse-tag-check)
             (org-unit-test-layer/parse-codeblock-check)
             )
  )
(defun org-unit-test-layer/parse-existence-test ()
  ;; length
  (org-unit-test-layer/parse-length-check)
  )
(defun org-unit-test-layer/parse-misc-test (keyword)
  ;; precede/cite/mention
  (cond
   ((equal keyword "precede") (org-unit-test-layer/parse-order-check))
   ((equal keyword "cite") (org-unit-test-layer/parse-citation-check))
   ((equal keyword "mention") (org-unit-test-layer/parse-mention-check))
   )
  )

(defun org-unit-test-layer/parse-section-check()
  (let ((test-type :section-check) subsection)
    (org-unit-test-layer/parse-sword "section")
    (setq subsection (org-unit-test-layer/parse-quote-string))
    (parsec-ch ?.)
    (make-org-unit-test-layer/test :type test-type :value (downcase subsection))
    )
  )
(defun org-unit-test-layer/parse-length-check ()
  (let ((test-type :length-check) dir length counter)
    (setq dir (parsec-or (org-unit-test-layer/parse-sword "larger" :larger) (org-unit-test-layer/parse-sword "smaller" :smaller)))
    (org-unit-test-layer/parse-sword "than")
    (setq length (string-to-number (parsec-return (parsec-re "[[:digit:]]+") (org-unit-test-layer/parse-space)))
          counter (parsec-or (org-unit-test-layer/parse-sword "words" :words) (org-unit-test-layer/parse-sword "paragraphs" :paras) (org-unit-test-layer/parse-sword "sections" :sects))
          )
    (parsec-ch ?.)
    (make-org-unit-test-layer/test :type test-type :value `(,dir ,length ,counter))
    )
  )
(defun org-unit-test-layer/parse-order-check ()
  ;; locator should precede heading
  (let ((test-type :order-check) second)
    (setq second (org-unit-test-layer/parse-quote-string))
    (parsec-ch ?.)
    (make-org-unit-test-layer/test :type test-type :value second)
    )
  )
(defun org-unit-test-layer/parse-citation-check ()
  ;; locator should cite citelist
  (let ((test-type :citation-check) citations)
    (parsec-ch ?:)
    (setq citations (parsec-sepby (parsec-re "[[:alnum:]]+") (parsec-ch ?,)))
    (parsec-ch ?.)
    (make-org-unit-test-layer/test :type test-type :value citations)
    )
  )
(defun org-unit-test-layer/parse-mention-check ()
  ;; locator should mention string
  (let ((test-type :mention-check) mention)
    (setq mention (org-unit-test-layer/parse-quote-string))
    (parsec-ch ?.)
    (make-org-unit-test-layer/test :type test-type :locator locator :value mention)
    )
  )
(defun org-unit-test-layer/parse-codeblock-check ()
  ;; locator should have a codeblock
  (let ((test-type :codeblock-check) language)
    (org-unit-test-layer/parse-sword "codeblock")
    (setq language (parsec-optional (parsec-and (org-unit-test-layer/parse-sword "in") (org-unit-test-layer/parse-quote-string))))
    (parsec-ch ?.)
    (make-org-unit-test-layer/test :type test-type :value language)
    )
  )
(defun org-unit-test-layer/parse-tag-check ()
  ;; locator should have tag x
  (let ((test-type :tag-check) mention)
    (org-unit-test-layer/parse-sword "tag")
    (setq mention (org-unit-test-layer/parse-quote-string))
    (parsec-ch ?.)
    (make-org-unit-test-layer/test :type test-type :locator locator :value mention)
    )
  )

;; Test Execution
(defun org-unit-test-layer/forward-to (locator bound)
  (cond
   ((eq :document locator) nil)
   ((eq :section locator) nil)
   (t (re-search-forward (format org-unit-test-layer/heading-regexp locator) bound))
   )
  )
(defun org-unit-test-layer/forward-past-tests (locator bound)
  (org-unit-test-layer/forward-to locator bound)
  (let ((orig (point)))
    (re-search-forward org-drawer-regexp bound)
    (if (s-equals? "__org-unit-test-layer__" (match-string 1))
        (re-search-forward org-drawer-regexp bound)
      (goto-char orig))
    )
  )

(defun org-unit-test-layer/run-tests (testgroups)
  (mapcar 'org-unit-test-layer/run-test-group testgroups)
  )
(defun org-unit-test-layer/run-test-group (testgroup)
  (assert (org-unit-test-layer/test-group-p testgroup) t)
  (goto-char (org-unit-test-layer/test-group-start testgroup))
  (let* ((name (org-unit-test-layer/test-group-name testgroup))
         (bound (org-unit-test-layer/test-group-bound testgroup))
         (results (mapcar (lambda (x) `(,(org-unit-test-layer/test-to-string x) . ,(org-unit-test-layer/run-test x bound))) (org-unit-test-layer/test-group-tests testgroup)))
         )
    (make-org-unit-test-layer/test-results :name name :results results :link (org-unit-test-layer/test-group-link testgroup)))
  )
(defun org-unit-test-layer/run-test (test bound)
  (assert (org-unit-test-layer/test-p test))
  (save-excursion
    (condition-case e
        (let ((type (org-unit-test-layer/test-type test)))
          (cond
           ((eq type :section-check )   (org-unit-test-layer/run-test-section test bound))
           ((eq type :length-check )    (org-unit-test-layer/run-test-length test bound))
           ((eq type :order-check )     (org-unit-test-layer/run-test-order test bound))
           ((eq type :citation-check )  (org-unit-test-layer/run-test-citation test bound))
           ((eq type :mention-check )    (org-unit-test-layer/run-test-mention test bound))
           ((eq type :codeblock-check ) (org-unit-test-layer/run-test-codeblock test bound))
           ((eq type :tag-check)        (org-unit-test-layer/run-test-tag test bound))
           (t (error "Unrecognized test type"))
           )
          )
      (search-failed nil)
      )
    )
  )

(defun org-unit-test-layer/run-test-section (test bound)
  ;;go to the locator
  (org-unit-test-layer/forward-past-tests (org-unit-test-layer/test-locator test) bound)
  ;; get subheadings
  (let ((subheadings (org-map-entries
                      (lambda () (downcase (substring-no-properties (org-get-heading)))) nil 'tree)))
    ;; is the section value in the subheadings?
    (-contains? subheadings (org-unit-test-layer/test-value test))
    )
  )
(defun org-unit-test-layer/run-test-length (test bound)
  (org-unit-test-layer/forward-past-tests (org-unit-test-layer/test-locator test) bound)
  (let* ((vals (org-unit-test-layer/test-value test))
         (dir (car vals))
         (length (cadr vals))
         (counter (caddr vals))
         (test (if (eq :larger dir) '> '<))
         )
    (cond
     ((eq counter :words) (funcall test (count-words (point)
                                                     (plist-get (cadr (org-element-at-point)) :contents-end))
                                   length))
     ;; ((eq counter :paras) (funcall test (count-paragraphs (point)
     ;;                                                      (plist-get (cadr (org-element-at-point)) :contents-end))
     ;;                               length)
     ((eq counter :sects)  (funcall test (- (reduce '+ (org-map-entries (lambda () 1) nil 'tree)) 1) length))
     (t (error "Unrecognized test-length counter"))
     )
    )
  )
(defun org-unit-test-layer/run-test-order (test bound)
  (let* ((tree (org-map-entries (lambda () (substring-no-properties (org-get-heading))) nil 'tree))
         (locator (-elem-index (org-unit-test-layer/test-locator test) tree))
         (section (-elem-index (org-unit-test-layer/test-value test) tree))
         )
    (if (and locator section)
        (< locator section)
      nil)
    )
  )
(defun org-unit-test-layer/run-test-citation (test bound)
  (org-unit-test-layer/forward-past-tests (org-unit-test-layer/test-locator test) bound)
  (let ((citations (org-unit-test-layer/test-value test)) curr-line)
    (while citations
      (re-search-forward "cite:" bound)
      (setq curr-line (buffer-substring-no-properties (line-beginning-position) (line-end-position)))
      (setq citations (remove-if (lambda (x) (s-contains? x curr-line)) citations))
      )
    )
  )
(defun org-unit-test-layer/run-test-mention (test bound)
  (org-unit-test-layer/forward-past-tests (org-unit-test-layer/test-locator test) bound)
  (search-forward (org-unit-test-layer/test-value test) bound)
  )
(defun org-unit-test-layer/run-test-codeblock (test bound)
  (org-unit-test-layer/forward-past-tests (org-unit-test-layer/test-locator test) bound)
  (re-search-forward (format "%s %s" org-unit-test-layer/src-block-regexp
                             (if (org-unit-test-layer/test-value test) (org-unit-test-layer/test-value test) "")) bound)
  )
(defun org-unit-test-layer/run-test-tag (test bound)
  (org-unit-test-layer/forward-to (org-unit-test-layer/test-locator test) bound)
  (let ((tags (plist-get (cadr (org-element-at-point)) :tags)))
    (-contains? tags (org-unit-test-layer/test-value test))
    )
  )

;; Test Reporting
(defun org-unit-test-layer/print-results (results)
  (with-temp-buffer-window "*Test Results*"
                           nil
                           nil
                           (princ "* Test Results\n")
                           (mapc 'org-unit-test-layer/print-test-results results)
                           (org-mode)
                           )
  (with-current-buffer "*Test Results*"
    (org-mode)
    (goto-char (point-min))
    (org-cycle)
    )
  )
(defun org-unit-test-layer/print-test-results (testgroup)
  (let ((successes (reduce '+ (mapcar (lambda (x) (if (cdr x) 1 0)) (org-unit-test-layer/test-results-results testgroup))))
        (total (length (org-unit-test-layer/test-results-results testgroup))))
    (assert (org-unit-test-layer/test-results-p testgroup))
    (princ (format "** %s: (%s / %s) \n" (org-unit-test-layer/test-results-name testgroup) successes total))
    (princ (format "    %s\n" (org-unit-test-layer/test-results-link testgroup)))
    (mapc (lambda (x) (princ (s-concat "    " (if (cdr x) "âœ“" "X") " : " (car x) "\n"))) (org-unit-test-layer/test-results-results testgroup))
    )
  )

;; Main Access
(defun org-unit-test-layer/test-org-file ()
  (interactive)
  ;; check file is in org mode
  (assert (eq major-mode 'org-mode))
  ;; get tests
  (let* ((test-texts (seq-filter 'identity (org-unit-test-layer/find-tests)))
         (parsed-tests (seq-filter 'identity (-flatten (mapcar 'org-unit-test-layer/parse-tests test-texts))))
         (test-results (org-unit-test-layer/run-tests parsed-tests))
         )
    ;; print test results
    (org-unit-test-layer/print-results test-results)
    )
  )
